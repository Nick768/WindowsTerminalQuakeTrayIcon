; make sure to use the correct version
#Requires AutoHotkey v2.0-

SetWorkingDir(A_ScriptDir)            ; not really needed; just to be save

; Language definitions
#Include Languages.ahk2

; detect language (defaults to english)
languageSelection := languages.en
if RegExMatch(A_Language, "..07")     ; every language code with xx07 means german
    languageSelection := languages.de


; place for configs
configDir  := EnvGet("LocalAppdata") . "\WindowsTerminalQuakeTrayIcon\"
configFile := configDir . "config.ini"


; pre-start checks (not necessary if not first start)
if IniRead(configFile, "Configs", "FirstStart", true) {
    if not DirExist(configDir)
        DirCreate(configDir)
    if not FileExist(configFile)
        FileAppend("
        (LTrim
            [Configs]
            ; Here you can tweak some basic settings

            ; FirstStart: Boolean (0 or 1; 0 means false, 1 means true)
            ; Whether the program was started more than once or not
            FirstStart=0

            [Hotkey]
            ; Here you can set a custom key-combo with up to three keys

            ; NOTE:
            ; You have to use the hotkeys defined by AutoHotkey V2 (read the docs)
            ; https://lexikos.github.io/v2/docs/Hotkeys.htm

            ; Some basic keys:
            ; Every letter or number can be used by defining it as it is
            ; CTRL can be used like this:
            ; LCtrl for the left and RCtrl for the right CTRL key
            ; LShift for the left and RShift for the right shift key
            ; Alt: LAlt for left Alt key / RAlt for right Alt key
            ; Windows key: LWin (or RWin - probably not useful)
            ; F1-F24 can be used as they are
            ; Esc can also be used as it is

            ; Key1: Key on your keyboard / first of maximal three keys to be pressed
            ; Key2: Key on your keyboard / first of maximal second keys to be pressed
            ; Key3: Key on your keyboard / first of maximal third keys to be pressed

            ; Default: LWin + LShift + `` (like in Windows Terminal)
            Key1=LWin
            Key2=LShift
            Key3=``
        )", configFile, "`n")
}


; uninstall:
; remove shirtcut from shell:startup
; delete configDir recursively
Uninstall() {
    if DirExist(configDir)
        DirDelete(configDir, true)

    return
}


terminalAction() {


    try
        Run("wt.exe -w _quake")
    catch
        if MsgBox(languageSelection.noWTError,, 20) = "yes" {
            Uninstall()
            ExitApp()
        }

    return
}


openSettings() {
    Run(configFile)
}


; code for tray icon
#Include TrayIcon.ahk2

Persistent(true)    ; only for testing
